apply plugin: 'maven-publish'
apply plugin: 'signing'
//apply plugin: 'org.jetbrains.dokka'

task androidSourcesJar(type: Jar) {
    archiveClassifier.set('sources')
    if (project.plugins.findPlugin("com.android.library")) {
        from android.sourceSets.main.java.srcDirs
        from android.sourceSets.main.kotlin.srcDirs
    } else {
        from sourceSets.main.java.srcDirs
        from sourceSets.main.kotlin.srcDirs
    }
}

//tasks.withType(dokkaHtmlPartial.getClass()).configureEach {
//    pluginsMapConfiguration.set(
//            ["org.jetbrains.dokka.base.DokkaBase": """{ "separateInheritedMembers": true}"""]
//    )
//}

task javadocJar(type: Jar) {
    archiveClassifier.set('javadoc')
//    from dokkaJavadoc.outputDirectory
}

artifacts {
    archives androidSourcesJar
    archives javadocJar
}

version = PUBLISH_VERSION

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                groupId rootProject.ext["groupId"]
                artifactId PUBLISH_ARTIFACT_ID
                version PUBLISH_VERSION
                if (project.plugins.findPlugin("com.android.library")) {
                    from components.release
                } else {
                    from components.java
                }

                artifact androidSourcesJar
                artifact javadocJar

                pom {
                    name = PUBLISH_ARTIFACT_ID
                    description = 'Safe compose args'
                    url = 'https://github.com/dilrajsingh1997/safe-compose-args'
                    licenses {
                        license {
                            name = 'Compose annotation processor license'
                            url = 'https://github.com/dilrajsingh1997/safe-compose-args/blob/main/LICENSE'
                        }
                    }
                    developers {
                        developer {
                            id = 'dilrajsingh1997'
                            name = 'Dilraj Singh'
                            email = 'singh.dilraj342@gmail.com'
                        }
                    }
                    scm {
                        connection = 'scm:git:github.com/dilrajsingh1997/safe-compose-args.git'
                        developerConnection = 'scm:git:ssh://github.com/dilrajsingh1997/safe-compose-args.git'
                        url = 'https://github.com/dilrajsingh1997/safe-compose-args/tree/main'
                    }
                }
            }
        }
    }
}

signing {
    useInMemoryPgpKeys(
            rootProject.ext["keyId"],
            rootProject.ext["key"],
            rootProject.ext["password"],
    )
    sign publishing.publications
}
